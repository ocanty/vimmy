1.4 â€¢ Functions
===================

A function is a block of code which takes parameters and returns a result
You define a function like how you define a label:

	myFunction:
		<my code>
		ret
		
However unlike a regular block of code, you simply do not jump to a function, you must instead use the `CALL` instruction

The CALL instruction will jump to the label and push the return address (the address that followed the CALL instruction) to the stack.
This address exists on the stack so you can call the `RET` instruction which will return you to normal execution.

	myFunction:
		mov a,1
		ret
	
	start:
		call myFunction
		
		; RET will bring us here
		jmp start

Parameters
--

Passing parameters is implementation defined, it is up to you to decide how you wish to do it. If you are looking for more information about this, [click here to see how calling conventions work in other assembly languages](https://en.wikibooks.org/wiki/X86_Disassembly/Calling_Conventions)

However, the recommended calling convention for the VM is discussed below

Parameters are passed by pushing them onto the stack, from left to right order. The caller of the function is responsible for cleaning up the stack when the function has returned.
You may store the return value in any register you wish however, `A` is typically recommended

## Function Prologue
Before you can begin accessing parameters it is advised to use a **function prologue**. A function prologue is used to set up the stack in a way that you can easily access passed parameters and create your own local variables. Here we introduce the `BP` register, the **base pointer**. It points to the base of the current **stack frame**. A stack frame is a group of data on the stack. You can use the base pointer with a memory offset to read up or down the stack as you wish. 

A typical function prologue is shown below:

	
	myFunction:
		; Function calls can be nested (i.e a function can be called inside a function),
		; But there is only one value allowed to be in the base pointer at a time
		; Therefore we must preserve a previous value if it was stored in the BP register
		; We accomplish this by pushing it onto the stack
		push bp
		
		; Copy the value of the stack pointer into the base pointer,
		; We will use this later as the stack grows we still can access values from the bottom of it
		; Doing this beings our "stack frame"
		; We will be able to use memory offsets from BP to access data
		mov bp, sp
		
		<your code to be executed here>
		
		; Restore the preserved BP value we saved earlier, and return
		pop bp
		ret
	
	start:
		call myFunction
		jmp start
		
## Accessing parameters
As mentioned earlier, passing parameters to a function is done by passing them in **left-to-right** order on the stack. This means that the for a function definition such as **myFunction(arg1,arg2)**, **arg1** will be pushed onto the stack first (as it is on the left side) followed by **arg2**. The code example shown below shows how to access passed parameters like these


	; sub_number(number,number)
	sub_number:
		; function prologue
		push bp
		mov bp, sp

		; the first arguments are the furthest away from the base pointer
		mov a,[bp+6]
		
		; the last argument passed is always at +4
		; this is because when the CALL instruction executes 
		; It pushes the return address that RET will jump to on the stack (+0)
		; additionally above we preserved BP by pushing it on the stack (+2)
		; therefore the last argument begins at +4
		; looking back above you will now understand that the next argument will be +6,+8,+10,...
		mov b,[bp+4]

		sub a,b

		; Restore the preserved BP value we saved earlier, and return
		pop bp
		ret

	start:
		; we want to do 5-3
		push 5
		push 3
		call sub_number
		
		; when your function returns you are expected to clean up the stack 
		; we pushed 2 16-bit numbers so we must remove the top two values from the stack
		; rather than popping them it is easier to simply add 2 to the SP for each value
		; this will cause them to be overwritten when other code needs to use the stack
		add sp, 4
		
		; a should now be 2
		
		jmp start
		
### [See it in action](../../../projects/06e503)

## Local Variables
If you require local variables you must first make space on the stack, this is simply accomplished by subtracting a value from `SP`. You can then use offsets from the base pointer to access and save variables as you wish.
**Before the function returns, you must clean up your variables or the return instruction will fail to work by jumping to an invalid location** Here is the previous function, but instead this time we simply created a variable and stored the value 5 in it.


	; sub_number(number,number)
	sub_number:
		push bp
		mov bp, sp
		
		; Make space for 1 16-bit variable on the stack
		sub sp, 2
		
		; The stack grows upward towards low memory addresses
		; We can use the base pointer to access these locations
		; Set our variable to 5
		mov [bp-2],5
		
		; You will be able to access this variable in further instructions as [bp-2]
		
		mov a,[bp+6]
		mov b,[bp+4]
		sub a,b
		

		pop bp
		; Clean up local variables
		; It is extremely important you do this right before RET or 
		; the instruction will fail possible causing the VM to crash 
		add sp,2
		ret

	start:
		push 5
		push 3
		call sub_number
		add sp, 4
		jmp start

### [See it in action](../../../projects/4fa9fc)
		
![Stack Diagram](/img/lesson-resource/stack-function.png)

The intended stack setup for a function call

## Preserving state
It is incredibly important to realise that when a function is called, it must use registers that may have been previously used before the function was called. Therefore when the function returns it must return the registers to their initial respective values as if the function had not been called. Typically as mentioned above, the register `A` is used for returning values, and no other register should be modified.

Preserving state as easy as simply pushing any register you want to use in the function onto the stack and preserve its value. When you are finished doing your processing and you want to return control, all you need to do is pop the registers back off the stack.

	; A function that will use the c register
	my_function:
		push bp
		mov bp, sp
		
		; We will be using the c register 
		; so we must preserve its previous value for when we return control
		; otherwise it will have unintended consequences
		push c
		
		; set a value in c
		mov c, 80
		
		; do something with c here
		
		; return c to its initial value by reading our preserved value off the stack
		pop c
		pop bp
		ret

	start:
		; we want c to remain as 5 before and after the function call
		mov c, 5

		call my_function
		
		; You should step forward to check c here in the debugger and verify that it remains 5
		
		jmp start

### [See it in action](../../../projects/4546ad)