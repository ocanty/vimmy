2.2 â€¢ The GPU
===================

The GPU operates on the I/O ports **0x9-0xF**. Commands are sent in the form of messages, with the message type loaded in 0x9, and arguments loaded in 0xA-0xF

Sample code is shown below that pushes a GPU message for you based off a function call, the function is shown unsimplified for you understanding

	; do_gpu_message(type,arg0,arg1,arg2,arg3,arg4,arg5)
	do_gpu_message:
		push bp
		mov bp, sp
		
		; load args
		mov c, 0xf
		mov d, [bp+4]
		out d,c
		
		mov c, 0xe
		mov d, [bp+6]
		out d,c
		
		mov c, 0xd
		mov d, [bp+8]
		out d,c
		
		mov c, 0xc
		mov d, [bp+10]
		out d,c
		
		mov c, 0xb
		mov d, [bp+12]
		out d,c
		
		mov c, 0xa
		mov d, [bp+14]
		out d,c
		
		
		; message type
		; loaded last as this write will send the command to the GPU.
		mov c,0x9
		mov d,[bp+16]
		out d,c
		
		pop bp
		ret
		
Framebuffer
----

All draw operations are performed on a **framebuffer**, when you are finished drawing you must send the flip message in order to display your rendered objects to the user. The framebuffer is used to prevent visual tearing or artifacts during rendering.

Colors
----

Colors are required to be in the data form of **RGB565**, [click here for a conversion tool between RGB888 and RGB565](http://www.barth-dev.de/online/rgb565-color-picker/). Please not that if you do use this converter you will need to convert it's result into the endianess of the CPU. (for example if you are given 0x33AA by the converter, it's correct format is 0xAA33)
		
Sprites
----
Adding a sprite in the data section will automatically convert it to the valid image format, you can then supply its label as part of the "Texture-DMA" argument.

Transparency Mask
----
The transparency mask feature will ignore all writes with the color of the set transparency mask, this can be useful for "faking" transparency in your draw operations or sprites. The default transparency mask is **0xF81F**.

GPU Messages
----

	// Message ID / Name						Description						ARG0		ARG1		ARG2		ARG3		ARG4		ARG5	
	[0x1001] =  vmhw_gpu_clear_framebuffer,	//	Clears the framebuffer			-			-			-			-			-			Color	
	[0x1002] =  vmhw_gpu_flip_framebuffer,	//	Flip framebuffer				-			-			-			-			-			-	
	                                        //	
	[0x1051] =  vmhw_gpu_enable_transmask,	//	Enable transparency mask		-			-			-			-			-			-	
	[0x1052] =  vmhw_gpu_disable_transmask,	//	Disable transparency mask		-			-			-			-			-			-	
	[0x1053] =  vmhw_gpu_set_transmask,		//	Set transparency mask			-			-			-			-			-			Color	
	                                        //	
	[0x1063] =  vmhw_gpu_set_text_terminal,	//  Set text on terminal			X-coord		Y-coord		-			-			Addr-DMA	Color	
	                                        //	
	[0x1100] =  vmhw_gpu_draw_rect,			//	Draw rectangle					X-coord		Y-coord		X2-coord	Y2-coord	-			Color	
	[0x1101] =  vmhw_gpu_draw_circle,		//	Draw circle						X-coord		Y-coord		-			-			Radius		Color	
	[0x1102] =  vmhw_gpu_draw_line,			//	Draw line						X1-coord	Y1-coord	X2-coord	Y2-coord	-			Color	
	[0x1103] =  vmhw_gpu_draw_sprite 		//	Draw sprite					    X-coord		Y-coord	    X2-coord	Y2-coord	Texture-DMA -	

In the cases with DMA, it typically refers to an area of data such as string or sprite
	
The Message Loop
----

You can use the GPU message driver shown above to send any messages you wish. The demo shown below will draw text

	do_gpu_message:
		push bp
		mov bp, sp

		; load args
		mov c, 0xf
		mov d, [bp+4]
		out d,c

		mov c, 0xe
		mov d, [bp+6]
		out d,c

		mov c, 0xd
		mov d, [bp+8]
		out d,c

		mov c, 0xc
		mov d, [bp+10]
		out d,c

		mov c, 0xb
		mov d, [bp+12]
		out d,c

		mov c, 0xa
		mov d, [bp+14]
		out d,c


		; message type
		; loaded last as this write will send the command to the gpu.
		mov c,0x9
		mov d,[bp+16]
		out d,c

		pop bp
		ret
	  
	start:
		jmp loop_
		jmp start
		
	loop_:
		; we only want this loop to execute every 100 cycles,
		; so we don't spam the gpu with unnecessary messages
		
		; store the number of cycles executed in a
		timer a
		; if the number of cycles is a multiple of 100 jump to our gpu code
		mod a, 100
		; if the difference is less than 10, we do this because we cant expect even cycles
		cmp a, 10
		jle gpu_work
		jmp loop_
		
	gpu_work:
		; clear framebuffer
		push 0x1001
		push 0
		push 0
		push 0
		push 0
		push 0
		; lets make it blue
		push 0xea29
		; dbghalt
		call do_gpu_message
		add sp,14
		
		; push text
		push 0x1063
		push 1
		push 1
		push 0
		push 0
		push s_ourtext
		; lighter blue
		push 0x2e6e
		call do_gpu_message
		add sp,14
		
		; flip the framebuffer
		push 0x1002
		push 0
		push 0
		push 0
		push 0
		push 0
		push 0
		call do_gpu_message
		add sp,14
		
		jmp loop_
		
### [See it in action](../../../projects/681c43)


![Demo](/img/lesson-resource/gpu-demo.png)

The Result
